buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.researchgate', name: 'gradle-release', version: '2.6.0'
        classpath 'gradle.plugin.org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.10.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

/** Plugins **/
plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "de.undercouch.download" version "4.1.1"
}


repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://oss.jfrog.org/artifactory/libs-snapshot" }
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
    flatDir {
        dirs 'localLibs'
    }
}

/** Package description **/
group = "org.grobid"
description = """Grobid module for extraction of superconductor information from scientific related literature"""

/** Dependencies **/

//configurations.all {
//    resolutionStrategy {
//        force 'javax.measure:unit-api:2.0-SNAPSHOT'
//    }
//}

dependencies {
    //Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:4.3'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-api-easymock:2.0.9'

    //GROBID
    implementation 'com.github.kermitt2:grobid:0.7.0'
//    implementation 'org.grobid:grobid-core:0.7.0'
//    implementation 'org.grobid:grobid-trainer:0.7.0'
    implementation 'xom:xom:1.3.7'
    // to be removed when using grobid from the maven central (non-snapshot)
    implementation "net.arnx:jsonic:1.3.10"
    implementation 'org.apache.lucene:lucene-analyzers-common:8.9.0'

    // grobid-quantities specific dependencies
//    implementation 'tech.units:indriya:1.3'
    implementation 'si.uom:si-units-java8:0.9'
    implementation 'si.uom:si-quantity:0.9'
    implementation 'tec.uom:uom-se:1.0.9'
    implementation 'systems.uom:systems-ucum-java8:0.9'
    implementation 'systems.uom:systems-unicode-java8:0.9'
    implementation 'systems.uom:systems-quantity:0.9'
    implementation 'systems.uom:systems-common:0.9'

    implementation 'com.googlecode.clearnlp:clearnlp:1.3.1'

    //Apache commons
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-pool:commons-pool:1.6'
    implementation 'org.apache.commons:commons-csv:1.8'
    implementation 'org.apache.commons:commons-text:1.9'

    //Dropwizard
    implementation "io.dropwizard:dropwizard-core:1.3.29"
    implementation 'io.dropwizard:dropwizard-jersey:1.3.29'
    implementation "io.dropwizard:dropwizard-assets:1.3.29"
    implementation "com.hubspot.dropwizard:dropwizard-guicier:1.3.5.2"
    implementation "io.dropwizard:dropwizard-testing:1.3.29"
    implementation "io.dropwizard:dropwizard-forms:1.3.29"
    implementation "io.dropwizard:dropwizard-client:1.3.29"
    implementation "io.dropwizard:dropwizard-auth:1.3.29"
    implementation "io.dropwizard.metrics:metrics-core:4.0.0"
    implementation "io.dropwizard.metrics:metrics-servlets:4.0.0"

    //Misc
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.apache.httpcomponents:httpmime:4.5.3'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava:1.9.10'

    implementation 'org.dkpro.statistics:dkpro-statistics:2.2.1'
    implementation 'org.dkpro.statistics:dkpro-statistics-agreement:2.2.1'

    // XML
    implementation 'org.codehaus.woodstox:stax2-api:3.1.4'
    implementation 'com.fasterxml.woodstox:woodstox-core:5.1.0'

    // Jep
    implementation 'black.ninia:jep:3.9.1'

    //Grobid-quantities
    implementation('org.grobid:grobid-quantities:0.6.1-SNAPSHOT') {
        transitive = false
    }
}


configurations {
    implementation.exclude group: "org.slf4j", module: "slf4j-jdk14"
    implementation.exclude group: 'org.slf4j', module: "slf4j-log4j12"
    implementation.exclude group: 'log4j', module: "log4j"
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}

/** Other configuration **/
sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    //    options.compilerArgs << "-Xlint:deprecation"
    //    options.compilerArgs << "-Xlint:unchecked"
}

sourceSets.main.resources {
    srcDirs = ["src/main/resources", "resources/config"]
}

test {
    exclude '**/**IntegrationTest**'
}

tasks.run.workingDir = rootProject.rootDir

mainClassName = 'org.grobid.service.GrobidSuperconductorsApplication'

run {
    args = ['server', 'resources/config/config.yml']
}


/** Training configuration **/

//def trainerTasks = [
//    //Training models
//    "train-superconductors": "org.grobid.trainer.SuperconductorsTrainer",
//    "train-material": "org.grobid.trainer.MaterialTrainer",
//    "train-entityLinker-material_tc": "org.grobid.trainer.EntityLinkerMaterialTcTrainer",
//    "train-entityLinker-tc-pressure": "org.grobid.trainer.EntityLinkerTcPressureTrainer",
//    "train-entityLinker-tc-meMethod": "org.grobid.trainer.EntityLinkerTcMeMethodTrainer"
//]
//
//trainerTasks.each { taskName, mainClassName ->
//    tasks.create(name: taskName, type: JavaExec, group: 'training') {
//        main = mainClassName
//        classpath = sourceSets.main.runtimeClasspath
//    }
//}

processResources {
    filesMatching('version.txt') {
        filter {
            it.replace('project.version', project.property('version'))
        }
    }
}

//// return the default value if the property has not been specified in command line
//ext.getArg = { propName, defaultVal ->
//    return project.hasProperty(propName) ? project.getProperty(propName) : defaultVal;
//}

/*** Packaging and distribution ***/

shadowJar {
    mainClassName = 'org.grobid.service.GrobidSuperconductorsApplication'
    archiveClassifier = 'onejar'
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

jar {
    enabled false
}

artifacts {
    archives shadowJar
//    archives jar
}

//distZip.archiveClassifier = 'application'
//distTar.archiveClassifier = 'application'

distZip.enabled = false
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

/** Model management **/
task copyModels(type: Copy) {
    from "${rootDir}/resources/models"
    include "**/*.wapiti"
    include "**/*.wapiti.gz"
    include "**/config.json"
    include "**/model_weights.hdf5"
    include "**/preprocessor.json"
    include "**/checkpoint"
    include "**/events.out.tfevents.*"
    include "**/model.ckpt-*"
    include "**/bert_config.json"
    include "**/vocab.txt"
    exclude "**/features-engineering/**"
    exclude "**/result-logs/**"
    into "${rootDir}/../grobid-home/models/"

    doLast {
        ant.gunzip(src: "${rootDir}/../grobid-home/models/superconductors/model.wapiti.gz", dest: "${rootDir}/../grobid-home/models/superconductors/")
    }
}

task installScibert(dependsOn: copyModels) {
    doLast {
        download {
            src "https://storagescienceminer.s3.eu-central-1.amazonaws.com/NIMS/superconductors-scibert.zip"
            dest "${rootDir}/../grobid-home/models/superconductors-scibert.zip"
            overwrite false        
        }
        ant.unzip(src: "${rootDir}/../grobid-home/models/superconductors-scibert.zip", dest: "${rootDir}/../grobid-home/models/")
    }
}


wrapper {
    gradleVersion "7.1.1"
}



